grf {
    grfid:                  "BY\01\01";
    name:                   string(STR_GRF_NAME);
    desc:                   string(STR_GRF_DESCRIPTION);
    url:                    string(STR_GRF_URL);
    version:                0;
    min_compatible_version: 0;

    param {
        param_replace_default_graphics {
            type: bool;
            name: string(STR_PARAM_REPLACE_DEFAULT_NAME);
            desc: string(STR_PARAM_REPLACE_DEFAULT_DESC);
            def_value: 1;
        }
    }
    param {
        param_signal_gui_row_1 {
            type: int;
            name: string(STR_PARAM_SIGNAL_ROW_1_NAME);
            desc: string(STR_PARAM_SIGNAL_ROW_1_DESC);
            def_value: 0;
            names: {
                0: string(STR_PARAM_VALUE_SEMAPHORE);
                1: string(STR_PARAM_VALUE_SEARCHLIGHT);
                2: string(STR_PARAM_VALUE_COLORLIGHT);
                3: string(STR_PARAM_VALUE_BOPOSITION);
            };
        }
    }
    param {
        param_signal_gui_row_2 {
            type: int;
            name: string(STR_PARAM_SIGNAL_ROW_2_NAME);
            desc: string(STR_PARAM_SIGNAL_ROW_2_DESC);
            def_value: 1;
            names: {
                0: string(STR_PARAM_VALUE_SEMAPHORE);
                1: string(STR_PARAM_VALUE_SEARCHLIGHT);
                2: string(STR_PARAM_VALUE_COLORLIGHT);
                3: string(STR_PARAM_VALUE_BOPOSITION);
            };
        }
    }
}

if (!extended_feature_test("action3_signals_custom_signal_sprites")) {
    error(FATAL, string(STR_UNSUPPORTED_VERSION));
}

// Commented by osswix and big mostly

// template for spriteset :
// spriteset([name], "file location"){[template_specific](x,y)}
// where file location is the location of the image containing the sprites
// template_specific is the template relevant to the sprites
// x and y are the starting coordinates of the first sprite in the set
//
// For this american set the following setup will be used
//
// For single aspect signals there will be three states:
// CLR  clear
// APR  approach
// STP  stop
//
// for two aspect signals there is a more extended variety of states,
// these states differe between block and path signals to somewhat
// emulate a realistic situation, the states are as follows
//
// for 2 aspect block signals:
// CLR  clear
// AAL  advanced approach limited
// APM  approach medium
// AAP  advanced approach
// APR  approach
// STP  stop
//
// for 2 aspect path signals:
// CLR  clear
// MCL  medium clear
// APR  approach
// STP  stop
//
// for 3 aspect block signals:
// CLR  clear
// AAL  advanced approach limited
// APM  approach medium
// APS  approach slow
// APR  approach
// STP  stop
//
// for 3 aspect path signals:
// CLR  clear
// LCL  limited clear
// MCL  medium clear
// SCL  slow clear
// STP  stop
//
// the following abbriviations are used
// SA, DA, TA for single, double, and triple aspect respectively
// B for block and P for path.
// S for semaphore and L for light with other variations to later be added.

/*
The graphics are organized into a grid.
Each column cycles between RYG, then by orientation.
Each row is exactly 3 aspects, based on secondary aspect.
The rows are ordered as:
- single-head
- double-head, G top
- double-head, Y top
- double-head, R top
*/

// template [template name](x, y){
//  [x, y, sizex, sizey, offsetx offsety]
// }

/*
Each signal is 16x32px, with 1 pixel in between on all sides.
=> Each signal has their top left corner on (17i, 33j), where i >= 0 and j >= 0.
Different orientations of the same aspect are located on the same row, 17*3 = 51px on the x-axis.
*/
template aspect_cell(x, y, n) { [17*x+51*n, 33*y, 16, 32, -8, -32] }

// x and y are the 0-indexed col/row number of the signal
template aspect(signalRow, aspectColumn) {
    aspect_cell(aspectColumn, signalRow, 0)
    aspect_cell(aspectColumn, signalRow, 1)
    aspect_cell(aspectColumn, signalRow, 2)
    aspect_cell(aspectColumn, signalRow, 3)
    aspect_cell(aspectColumn, signalRow, 4)
    aspect_cell(aspectColumn, signalRow, 5)
    aspect_cell(aspectColumn, signalRow, 6)
    aspect_cell(aspectColumn, signalRow, 7)
}

spriteset(signal_single_block_semaphore_CLR, "gfx/1-and-2-head.png"){aspect(0, 2)}
spriteset(signal_single_block_semaphore_APR, "gfx/1-and-2-head.png"){aspect(0, 1)}
spriteset(signal_single_block_semaphore_STP, "gfx/1-and-2-head.png"){aspect(0, 0)}

spriteset(signal_double_block_semaphore_CLR, "gfx/1-and-2-head.png"){aspect(1, 2) aspect(1, 0)}
spriteset(signal_double_block_semaphore_AAL, "gfx/1-and-2-head.png"){aspect(1, 1)}
spriteset(signal_double_block_semaphore_APM, "gfx/1-and-2-head.png"){aspect(2, 1)}
spriteset(signal_double_block_semaphore_AAP, "gfx/1-and-2-head.png"){aspect(2, 2)}
spriteset(signal_double_block_semaphore_APR, "gfx/1-and-2-head.png"){aspect(2, 0)}
spriteset(signal_double_block_semaphore_STP, "gfx/1-and-2-head.png"){aspect(3, 0)}

spriteset(signal_double_path_semaphore_CLR, "gfx/1-and-2-head.png"){aspect(1, 2) aspect(1, 0)}
spriteset(signal_double_path_semaphore_MCL, "gfx/1-and-2-head.png"){aspect(3, 2)}
spriteset(signal_double_path_semaphore_APR, "gfx/1-and-2-head.png"){aspect(2, 0)}
spriteset(signal_double_path_semaphore_STP, "gfx/1-and-2-head.png"){aspect(3, 0)}

spriteset(signal_double_onewaypath_semaphore_CLR, "gfx/1-and-2-head.png"){aspect(1, 26) aspect(1, 24)}
spriteset(signal_double_onewaypath_semaphore_MCL, "gfx/1-and-2-head.png"){aspect(3, 26)}
spriteset(signal_double_onewaypath_semaphore_APR, "gfx/1-and-2-head.png"){aspect(2, 24)}
spriteset(signal_double_onewaypath_semaphore_STP, "gfx/1-and-2-head.png"){aspect(3, 24)}

spriteset(signal_single_block_searchlight_CLR, "gfx/1-and-2-head.png"){aspect(4, 2)}
spriteset(signal_single_block_searchlight_APR, "gfx/1-and-2-head.png"){aspect(4, 1)}
spriteset(signal_single_block_searchlight_STP, "gfx/1-and-2-head.png"){aspect(4, 0)}

spriteset(signal_double_block_searchlight_CLR, "gfx/1-and-2-head.png"){aspect(5, 2) aspect(5, 0)}
spriteset(signal_double_block_searchlight_AAL, "gfx/1-and-2-head.png"){aspect(5, 1)}
spriteset(signal_double_block_searchlight_APM, "gfx/1-and-2-head.png"){aspect(6, 1)}
spriteset(signal_double_block_searchlight_AAP, "gfx/1-and-2-head.png"){aspect(6, 2)}
spriteset(signal_double_block_searchlight_APR, "gfx/1-and-2-head.png"){aspect(6, 0)}
spriteset(signal_double_block_searchlight_STP, "gfx/1-and-2-head.png"){aspect(7, 0)}

spriteset(signal_double_path_searchlight_CLR, "gfx/1-and-2-head.png"){aspect(5, 2) aspect(5, 0)}
spriteset(signal_double_path_searchlight_MCL, "gfx/1-and-2-head.png"){aspect(7, 2)}
spriteset(signal_double_path_searchlight_APR, "gfx/1-and-2-head.png"){aspect(6, 0)}
spriteset(signal_double_path_searchlight_STP, "gfx/1-and-2-head.png"){aspect(7, 0)}

spriteset(signal_double_onewaypath_searchlight_CLR, "gfx/1-and-2-head.png"){aspect(5, 26) aspect(5, 24)}
spriteset(signal_double_onewaypath_searchlight_MCL, "gfx/1-and-2-head.png"){aspect(7, 26)}
spriteset(signal_double_onewaypath_searchlight_APR, "gfx/1-and-2-head.png"){aspect(6, 24)}
spriteset(signal_double_onewaypath_searchlight_STP, "gfx/1-and-2-head.png"){aspect(7, 24)}

spriteset(signal_single_block_colorlight_CLR, "gfx/1-and-2-head.png"){aspect(8, 2)}
spriteset(signal_single_block_colorlight_APR, "gfx/1-and-2-head.png"){aspect(8, 1)}
spriteset(signal_single_block_colorlight_STP, "gfx/1-and-2-head.png"){aspect(8, 0)}

spriteset(signal_double_block_colorlight_CLR, "gfx/1-and-2-head.png"){aspect(9, 2) aspect(9, 0)}
spriteset(signal_double_block_colorlight_AAL, "gfx/1-and-2-head.png"){aspect(9, 1)}
spriteset(signal_double_block_colorlight_APM, "gfx/1-and-2-head.png"){aspect(10, 1)}
spriteset(signal_double_block_colorlight_AAP, "gfx/1-and-2-head.png"){aspect(10, 2)}
spriteset(signal_double_block_colorlight_APR, "gfx/1-and-2-head.png"){aspect(10, 0)}
spriteset(signal_double_block_colorlight_STP, "gfx/1-and-2-head.png"){aspect(11, 0)}

spriteset(signal_double_path_colorlight_CLR, "gfx/1-and-2-head.png"){aspect(9, 2) aspect(9, 0)}
spriteset(signal_double_path_colorlight_MCL, "gfx/1-and-2-head.png"){aspect(11, 2)}
spriteset(signal_double_path_colorlight_APR, "gfx/1-and-2-head.png"){aspect(10, 0)}
spriteset(signal_double_path_colorlight_STP, "gfx/1-and-2-head.png"){aspect(11, 0)}

spriteset(signal_double_onewaypath_colorlight_CLR, "gfx/1-and-2-head.png"){aspect(9, 26) aspect(9, 24)}
spriteset(signal_double_onewaypath_colorlight_MCL, "gfx/1-and-2-head.png"){aspect(11, 26)}
spriteset(signal_double_onewaypath_colorlight_APR, "gfx/1-and-2-head.png"){aspect(10, 24)}
spriteset(signal_double_onewaypath_colorlight_STP, "gfx/1-and-2-head.png"){aspect(11, 24)}


spriteset(signal_single_block_boposition_CLR, "gfx/1-and-2-head.png"){aspect(12, 2)}
spriteset(signal_single_block_boposition_APR, "gfx/1-and-2-head.png"){aspect(12, 1)}
spriteset(signal_single_block_boposition_STP, "gfx/1-and-2-head.png"){aspect(12, 0)}

spriteset(signal_double_block_boposition_CLR, "gfx/1-and-2-head.png"){aspect(13, 15)}
spriteset(signal_double_block_boposition_AAL, "gfx/1-and-2-head.png"){aspect(13, 14)}
spriteset(signal_double_block_boposition_APM, "gfx/1-and-2-head.png"){aspect(13, 14)}
spriteset(signal_double_block_boposition_AAP, "gfx/1-and-2-head.png"){aspect(13, 14)}
spriteset(signal_double_block_boposition_APR, "gfx/1-and-2-head.png"){aspect(13, 14)}
spriteset(signal_double_block_boposition_STP, "gfx/1-and-2-head.png"){aspect(13, 13)}

spriteset(signal_double_path_boposition_CLR, "gfx/1-and-2-head.png"){aspect(13, 2)}
spriteset(signal_double_path_boposition_MCL, "gfx/1-and-2-head.png"){aspect(13, 1)}
spriteset(signal_double_path_boposition_APR, "gfx/1-and-2-head.png"){aspect(13, 1)}
spriteset(signal_double_path_boposition_STP, "gfx/1-and-2-head.png"){aspect(13, 0)}

spriteset(signal_double_onewaypath_boposition_CLR, "gfx/1-and-2-head.png"){aspect(13, 26)}
spriteset(signal_double_onewaypath_boposition_MCL, "gfx/1-and-2-head.png"){aspect(13, 25)}
spriteset(signal_double_onewaypath_boposition_APR, "gfx/1-and-2-head.png"){aspect(13, 25)}
spriteset(signal_double_onewaypath_boposition_STP, "gfx/1-and-2-head.png"){aspect(13, 24)}

//Switches use the following format:
//switch(FEAT_SIGNALS, SELF, [name], [variable]{
//  value: result;
//  value: result;
//  return result; //Giving a "return result" at the end will give a default value when none of the other values have been met by the variable
//}
//The name can be any text string you like.
//The variables are pre-selected here to give the correct results
//The result can be a spriteset or another switch
//
//This switch decides which spriteset to use for each signal aspect for one signal type
//
//These are the templates of all switches in 'chronological' order:
//
// first bit is order of aspects, with a maximum of 7 (0-6)
//switch(FEAT_SIGNALS, SELF, newname, getbits(extra_callback_info2, 0, 8)){
//  00: stop signal;
//  01: highest clearance;
//  05: one step lower:
//  04: one step lower:
//  03: one step lower:
//  02: one step lower:
//}
//switch(FEAT_SIGNALS, SELF, newname, getbits(extra_callback_info2, 8, 8)){
//  00: signal;
//  01: semaphore;
//}
//
//  note in the following programmable and no entry need to be enabled
//switch(FEAT_SIGNALS, SELF, newname, getbits(extra_callback_info2, 16, 8)){
//  00: normal;
//  01: entry;
//  02: exit;
//  03: combo;
//  04: path;
//  05: path one way;
//  06: programmable;
//  07: no entry;
//}
//(getbits(extra_callback_info2, 16, 8)

switch(FEAT_SIGNALS, SELF, sw_single_block_semaphore, getbits(extra_callback_info2, 0, 8)){
    00: signal_single_block_semaphore_STP;
    01: signal_single_block_semaphore_CLR;
    05: signal_single_block_semaphore_CLR;
    04: signal_single_block_semaphore_CLR;
    03: signal_single_block_semaphore_CLR;
    02: signal_single_block_semaphore_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_semaphore, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_block_semaphore_STP;
    01: signal_double_block_semaphore_CLR;
    05: signal_double_block_semaphore_AAL;
    04: signal_double_block_semaphore_APM;
    03: signal_double_block_semaphore_AAP;
    02: signal_double_block_semaphore_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_semaphore, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_path_semaphore_STP;
    01: signal_double_path_semaphore_CLR;
    05: signal_double_path_semaphore_CLR;
    04: signal_double_path_semaphore_CLR;
    03: signal_double_path_semaphore_MCL;
    02: signal_double_path_semaphore_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_semaphore, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_onewaypath_semaphore_STP;
    01: signal_double_onewaypath_semaphore_CLR;
    05: signal_double_onewaypath_semaphore_CLR;
    04: signal_double_onewaypath_semaphore_CLR;
    03: signal_double_onewaypath_semaphore_MCL;
    02: signal_double_onewaypath_semaphore_APR;
}

switch(FEAT_SIGNALS, SELF, sw_semaphore, getbits(extra_callback_info2, 16, 8)){
    0: sw_single_block_semaphore;
    4: sw_double_path_semaphore;
    5: sw_double_onewaypath_semaphore;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_searchlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_single_block_searchlight_STP;
    01: signal_single_block_searchlight_CLR;
    05: signal_single_block_searchlight_CLR;
    04: signal_single_block_searchlight_CLR;
    03: signal_single_block_searchlight_CLR;
    02: signal_single_block_searchlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_searchlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_block_searchlight_STP;
    01: signal_double_block_searchlight_CLR;
    05: signal_double_block_searchlight_AAL;
    04: signal_double_block_searchlight_APM;
    03: signal_double_block_searchlight_AAP;
    02: signal_double_block_searchlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_searchlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_path_searchlight_STP;
    01: signal_double_path_searchlight_CLR;
    05: signal_double_path_searchlight_CLR;
    04: signal_double_path_searchlight_CLR;
    03: signal_double_path_searchlight_MCL;
    02: signal_double_path_searchlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_searchlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_onewaypath_searchlight_STP;
    01: signal_double_onewaypath_searchlight_CLR;
    05: signal_double_onewaypath_searchlight_CLR;
    04: signal_double_onewaypath_searchlight_CLR;
    03: signal_double_onewaypath_searchlight_MCL;
    02: signal_double_onewaypath_searchlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_searchlight, getbits(extra_callback_info2, 16, 8)){
    0: sw_single_block_searchlight;
    4: sw_double_path_searchlight;
    5: sw_double_onewaypath_searchlight;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_colorlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_single_block_colorlight_STP;
    01: signal_single_block_colorlight_CLR;
    05: signal_single_block_colorlight_CLR;
    04: signal_single_block_colorlight_CLR;
    03: signal_single_block_colorlight_CLR;
    02: signal_single_block_colorlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_colorlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_block_colorlight_STP;
    01: signal_double_block_colorlight_CLR;
    05: signal_double_block_colorlight_AAL;
    04: signal_double_block_colorlight_APM;
    03: signal_double_block_colorlight_AAP;
    02: signal_double_block_colorlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_colorlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_path_colorlight_STP;
    01: signal_double_path_colorlight_CLR;
    05: signal_double_path_colorlight_CLR;
    04: signal_double_path_colorlight_CLR;
    03: signal_double_path_colorlight_MCL;
    02: signal_double_path_colorlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_colorlight, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_onewaypath_colorlight_STP;
    01: signal_double_onewaypath_colorlight_CLR;
    05: signal_double_onewaypath_colorlight_CLR;
    04: signal_double_onewaypath_colorlight_CLR;
    03: signal_double_onewaypath_colorlight_MCL;
    02: signal_double_onewaypath_colorlight_APR;
}

switch(FEAT_SIGNALS, SELF, sw_colorlight, getbits(extra_callback_info2, 16, 8)){
    0: sw_single_block_colorlight;
    4: sw_double_path_colorlight;
    5: sw_double_onewaypath_colorlight;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_boposition, getbits(extra_callback_info2, 0, 8)){
    00: signal_single_block_boposition_STP;
    01: signal_single_block_boposition_CLR;
    05: signal_single_block_boposition_CLR;
    04: signal_single_block_boposition_CLR;
    03: signal_single_block_boposition_CLR;
    02: signal_single_block_boposition_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_boposition, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_block_boposition_STP;
    01: signal_double_block_boposition_CLR;
    05: signal_double_block_boposition_AAL;
    04: signal_double_block_boposition_APM;
    03: signal_double_block_boposition_AAP;
    02: signal_double_block_boposition_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_boposition, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_path_boposition_STP;
    01: signal_double_path_boposition_CLR;
    05: signal_double_path_boposition_CLR;
    04: signal_double_path_boposition_CLR;
    03: signal_double_path_boposition_MCL;
    02: signal_double_path_boposition_APR;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_boposition, getbits(extra_callback_info2, 0, 8)){
    00: signal_double_onewaypath_boposition_STP;
    01: signal_double_onewaypath_boposition_CLR;
    05: signal_double_onewaypath_boposition_CLR;
    04: signal_double_onewaypath_boposition_CLR;
    03: signal_double_onewaypath_boposition_MCL;
    02: signal_double_onewaypath_boposition_APR;
}

switch(FEAT_SIGNALS, SELF, sw_boposition, getbits(extra_callback_info2, 16, 8)){
    0: sw_single_block_boposition;
    4: sw_double_path_boposition;
    5: sw_double_onewaypath_boposition;
}

switch(FEAT_SIGNALS, SELF, listed_signals, signal_style) {
    1: sw_semaphore;
    2: sw_searchlight;
    3: sw_colorlight;
    4: sw_boposition;
}

template alternate_red_green(signalRow, aspectColumn) {
    aspect_cell(aspectColumn  , signalRow, 0)
    aspect_cell(aspectColumn+2, signalRow, 0)
    aspect_cell(aspectColumn  , signalRow, 1)
    aspect_cell(aspectColumn+2, signalRow, 1)
    aspect_cell(aspectColumn  , signalRow, 2)
    aspect_cell(aspectColumn+2, signalRow, 2)
    aspect_cell(aspectColumn  , signalRow, 3)
    aspect_cell(aspectColumn+2, signalRow, 3)
    aspect_cell(aspectColumn  , signalRow, 4)
    aspect_cell(aspectColumn+2, signalRow, 4)
    aspect_cell(aspectColumn  , signalRow, 5)
    aspect_cell(aspectColumn+2, signalRow, 5)
    aspect_cell(aspectColumn  , signalRow, 6)
    aspect_cell(aspectColumn+2, signalRow, 6)
    aspect_cell(aspectColumn  , signalRow, 7)
    aspect_cell(aspectColumn+2, signalRow, 7)
}

// signals switch
// 0xXX00 = signal variant
// 0x00XX = signal type
switch (FEAT_SIGNALS, SELF, sw_signals, (extra_callback_info2 & 0x00FFFF00)>>8) {
    0x0000: sw_single_block_searchlight;
    0x0001: sw_single_block_semaphore;
    0x0002: sw_single_block_colorlight;
    0x0003: sw_single_block_boposition;
    0x0400: sw_double_path_searchlight;
    0x0401: sw_double_path_semaphore;
    0x0402: sw_double_path_colorlight;
    0x0403: sw_double_path_boposition;
    0x0500: sw_double_onewaypath_searchlight;
    0x0501: sw_double_onewaypath_semaphore;
    0x0502: sw_double_onewaypath_colorlight;
    0x0503: sw_double_onewaypath_boposition;
    CB_FAILED;
}

if (param_replace_default_graphics) {
    // replace the semaphore bar
    // semaphore block = 48
    // semaphore path+one-way = 176
    if (param_signal_gui_row_1 == 0) { // semaphore
        replacenew replace_semaphore_block_semaphore (SIGNALS, "gfx/1-and-2-head.png", 48) {
            alternate_red_green(0, 0)
        }
        replacenew replace_semaphore_path_semaphore (SIGNALS, "gfx/1-and-2-head.png", 176) {
            alternate_red_green(3,  0)
            alternate_red_green(3, 24)
        }
    } else if (param_signal_gui_row_1 == 1) { // search
        replacenew replace_semaphore_block_search (SIGNALS, "gfx/1-and-2-head.png", 48) {
            alternate_red_green(4, 0)
        }
        replacenew replace_semaphore_path_search (SIGNALS, "gfx/1-and-2-head.png", 176) {
            alternate_red_green(7,  0)
            alternate_red_green(7, 24)
        }
    } else if (param_signal_gui_row_1 == 2) { // color
        replacenew replace_semaphore_block_color (SIGNALS, "gfx/1-and-2-head.png", 48) {
            alternate_red_green(8, 0)
        }
        replacenew replace_semaphore_path_color (SIGNALS, "gfx/1-and-2-head.png", 176) {
            alternate_red_green(11,  0)
            alternate_red_green(11, 24)
        }
    } else if (param_signal_gui_row_1 == 3) { // b&o
        replacenew replace_semaphore_block_bo (SIGNALS, "gfx/1-and-2-head.png", 48) {
            alternate_red_green(12, 0)
        }
        replacenew replace_semaphore_path_bo (SIGNALS, "gfx/1-and-2-head.png", 176) {
            alternate_red_green(13,  0)
            alternate_red_green(13, 24)
        }
    }
    // replace old electric block signals
    // electric block signals start at offset 1275
    // electric path+one-way = 112
    // also expects alternating red-green
    if (param_signal_gui_row_2 == 0) { // semaphore
        replace replace_electric_block_semaphore (1275, "gfx/1-and-2-head.png") {
            alternate_red_green(0, 0)
        }
        replacenew replace_electric_path_semaphore (SIGNALS, "gfx/1-and-2-head.png", 112) {
            alternate_red_green(3,  0)
            alternate_red_green(3, 24)
        }
    } else if (param_signal_gui_row_2 == 1) { // search
        replace replace_electric_block_searchlight (1275, "gfx/1-and-2-head.png") {
            alternate_red_green(4, 0)
        }
        replacenew replace_electric_path_searchlight (SIGNALS, "gfx/1-and-2-head.png", 112) {
            alternate_red_green(7,  0)
            alternate_red_green(7, 24)
        }
    } else if (param_signal_gui_row_2 == 2) { // color
        replace replace_electric_block_colorlight (1275, "gfx/1-and-2-head.png") {
            alternate_red_green(8, 0)
        }
        replacenew replace_electric_path_colorlight (SIGNALS, "gfx/1-and-2-head.png", 112) {
            alternate_red_green(11,  0)
            alternate_red_green(11, 24)
        }
    } else if (param_signal_gui_row_2 == 3) { // b&o
        replace replace_electric_block_bo (1275, "gfx/1-and-2-head.png") {
            alternate_red_green(12, 0)
        }
        replacenew replace_electric_path_bo (SIGNALS, "gfx/1-and-2-head.png", 112) {
            alternate_red_green(13,  0)
            alternate_red_green(13, 24)
        }
    }

    item (FEAT_SIGNALS, custom_signals, 0) {
        property {
            extra_aspects: 4;
        }
    }
} else {
    item (FEAT_SIGNALS, custom_signals, 0) {
        property {
            extra_aspects:      4;

            define_style:       1; // semaphore
            style_name:         string(STR_SIG_SEMAPHORE);
            style_semaphore_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY);

            define_style:       2; // searchlight
            style_name:         string(STR_SIG_SEARCHLIGHT);
            style_electric_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY);
            
            define_style:       3; // colorlight
            style_name:         string(STR_SIG_COLORLIGHT);
            style_electric_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY);
            
            define_style:       4; // b&o color position
            style_name:         string(STR_SIG_BOPOSITION);
            style_electric_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY);
        }
        graphics {
            listed_signals;
        }
    }
}
