grf {
    grfid:                  "BY\01\01";
    name:                   string(STR_GRF_NAME);
    desc:                   string(STR_GRF_DESCRIPTION);
    url:                    string(STR_GRF_URL);
    version:                7;
    min_compatible_version: 0;

    param {
        param_replace_default_graphics {
            type: bool;
            name: string(STR_PARAM_REPLACE_DEFAULT_NAME);
            desc: string(STR_PARAM_REPLACE_DEFAULT_DESC);
            def_value: 0;
        }
    }
    param {
        param_use_double_headed_block {
            type: bool;
            name: string(STR_PARAM_USE_DOUBLE_BLOCK_NAME);
            desc: string(STR_PARAM_USE_DOUBLE_BLOCK_DESC);
            def_value: 0;
        }
    }
    param {
        param_signal_gui_row_1 {
            type: int;
            name: string(STR_PARAM_SIGNAL_ROW_1_NAME);
            desc: string(STR_PARAM_SIGNAL_ROW_1_DESC);
            def_value: 0;
            min_value: 0;
            max_value: 3;
            names: {
                0: string(STR_PARAM_VALUE_SEMAPHORE);
                1: string(STR_PARAM_VALUE_SEARCHLIGHT);
                2: string(STR_PARAM_VALUE_COLORLIGHT);
                3: string(STR_PARAM_VALUE_BOPOSITION);
            };
        }
    }
    param {
        param_signal_gui_row_2 {
            type: int;
            name: string(STR_PARAM_SIGNAL_ROW_2_NAME);
            desc: string(STR_PARAM_SIGNAL_ROW_2_DESC);
            def_value: 1;
            min_value: 0;
            max_value: 3;
            names: {
                0: string(STR_PARAM_VALUE_SEMAPHORE);
                1: string(STR_PARAM_VALUE_SEARCHLIGHT);
                2: string(STR_PARAM_VALUE_COLORLIGHT);
                3: string(STR_PARAM_VALUE_BOPOSITION);
            };
        }
    }
}

// if (grf_future_status("\4E\4D\07\05")) { error(FATAL, string(STR_ERROR_INCOMPATIBLE_SET, "North American Track Set")); }
// if (grf_future_status("\4E\4D\09\20")) { error(FATAL, string(STR_ERROR_INCOMPATIBLE_SET, "North American Track Set 2")); }

if (!extended_feature_test("action3_signals_custom_signal_sprites")) {
    error(FATAL, string(STR_ERROR_UNSUPPORTED_VERSION));
}

// Commented by osswix and big mostly

// template for spriteset :
// spriteset([name], "file location"){[template_specific](x,y)}
// where file location is the location of the image containing the sprites
// template_specific is the template relevant to the sprites
// x and y are the starting coordinates of the first sprite in the set
//
// For this american set the following setup will be used
//
// For single aspect signals there will be three states:
// CLEAR  clear
// APPROACH  approach
// STOP  stop
//
// for two aspect signals there is a more extended variety of states,
// these states differe between block and path signals to somewhat
// emulate a realistic situation, the states are as follows
//
// for 2 aspect block signals:
// CLEAR  clear
// ADVANCED_APPROACH_LIMITED  advanced approach limited
// APPROACH_LIMITED  approach medium
// ADVANCED_APPROACH  advanced approach
// APPROACH  approach
// STOP  stop
//
// for 2 aspect path signals:
// CLEAR  clear
// MEDIUM_CLEAR  medium clear
// APPROACH  approach
// STOP  stop
//
// for 3 aspect block signals:
// CLEAR  clear
// ADVANCED_APPROACH_LIMITED  advanced approach limited
// APPROACH_LIMITED  approach medium
// APPROACH_SLOW  approach slow
// APPROACH  approach
// STOP  stop
//
// for 3 aspect path signals:
// CLEAR  clear
// LIMITED_CLEAR  limited clear
// MEDIUM_CLEAR  medium clear
// SLOW_CLEAR  slow clear
// STOP  stop
//
// the following abbriviations are used
// SA, DA, TA for single, double, and triple aspect respectively
// B for block and P for path.
// S for semaphore and L for light with other variations to later be added.

/*
The graphics are organized into a grid.
Each column cycles between RYG, then by orientation.
Each row is exactly 3 aspects, based on secondary aspect.
The rows are ordered as:
- single-head
- double-head, G top
- double-head, Y top
- double-head, R top
*/

// template [template name](x, y){
//  [x, y, sizex, sizey, offsetx offsety]
// }

/*
Each signal is 16x32px, with 1 pixel in between on all sides.
=> Each signal has their top left corner on (17i, 33j), where i >= 0 and j >= 0.
Different orientations of the same aspect are located on the same row, 17*3 = 51px on the x-axis.
*/
template cell(x, y, n, k) { [17*x+17*k*n, 33*y, 16, 32, -8, -32] }
template aspect_cell(x, y, n) { cell(x, y, n, 3) }

// x and y are the 0-indexed col/row number of the signal
template aspect(signalRow, aspectColumn) {
    aspect_cell(aspectColumn, signalRow, 0)
    aspect_cell(aspectColumn, signalRow, 1)
    aspect_cell(aspectColumn, signalRow, 2)
    aspect_cell(aspectColumn, signalRow, 3)
    aspect_cell(aspectColumn, signalRow, 4)
    aspect_cell(aspectColumn, signalRow, 5)
    aspect_cell(aspectColumn, signalRow, 6)
    aspect_cell(aspectColumn, signalRow, 7)
}
template consecutive(signalRow, aspectColumn) {
    cell(aspectColumn, signalRow, 0, 1)
    cell(aspectColumn, signalRow, 1, 1)
    cell(aspectColumn, signalRow, 2, 1)
    cell(aspectColumn, signalRow, 3, 1)
    cell(aspectColumn, signalRow, 4, 1)
    cell(aspectColumn, signalRow, 5, 1)
    cell(aspectColumn, signalRow, 6, 1)
    cell(aspectColumn, signalRow, 7, 1)
}

//!SPRITES!//

spriteset(signal_noentry, "src/img/oneway/oneway.png"){consecutive(0,0)}

//Switches use the following format:
//switch(FEAT_SIGNALS, SELF, [name], [variable]{
//  value: result;
//  value: result;
//  return result; //Giving a "return result" at the end will give a default value when none of the other values have been met by the variable
//}
//The name can be any text string you like.
//The variables are pre-selected here to give the correct results
//The result can be a spriteset or another switch
//
//This switch decides which spriteset to use for each signal aspect for one signal type
//
//These are the templates of all switches in 'chronological' order:
//
// first bit is order of aspects, with a maximum of 7 (0-6)
//switch(FEAT_SIGNALS, SELF, newname, getbits(extra_callback_info2, 0, 8)){
//  00: stop signal;
//  01: highest clearance;
//  05: one step lower:
//  04: one step lower:
//  03: one step lower:
//  02: one step lower:
//}
//switch(FEAT_SIGNALS, SELF, newname, getbits(extra_callback_info2, 8, 8)){
//  00: signal;
//  01: semaphore;
//}
//
//  note in the following programmable and no entry need to be enabled
//switch(FEAT_SIGNALS, SELF, newname, getbits(extra_callback_info2, 16, 8)){
//  00: normal;
//  01: entry;
//  02: exit;
//  03: combo;
//  04: path;
//  05: path one way;
//  06: programmable;
//  07: no entry;
//}

switch(FEAT_SIGNALS, SELF, SIGNAL_ASPECT, getbits(extra_callback_info2, 0, 8)){
    return;
}
switch(FEAT_SIGNALS, SELF, SIGNAL_VARIANT, getbits(extra_callback_info2, 8, 8)){
    return;
}
switch(FEAT_SIGNALS, SELF, SIGNAL_TYPE, getbits(extra_callback_info2, 16, 8)){
    return;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_semaphore, SIGNAL_ASPECT()){
    01: signal_single_block_semaphore_CLEAR;
    05: signal_single_block_semaphore_CLEAR;
    04: signal_single_block_semaphore_CLEAR;
    03: signal_single_block_semaphore_CLEAR;
    02: signal_single_block_semaphore_APPROACH;
    00: signal_single_block_semaphore_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_semaphore, SIGNAL_ASPECT()){
    01: signal_double_block_semaphore_CLEAR;
    05: signal_double_block_semaphore_ADVANCED_APPROACH_LIMITED;
    04: signal_double_block_semaphore_APPROACH_LIMITED;
    03: signal_double_block_semaphore_ADVANCED_APPROACH;
    02: signal_double_block_semaphore_APPROACH;
    00: signal_double_block_semaphore_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_semaphore, SIGNAL_ASPECT()){
    01: signal_double_path_semaphore_CLEAR;
    05: signal_double_path_semaphore_CLEAR;
    04: signal_double_path_semaphore_CLEAR;
    03: signal_double_path_semaphore_MEDIUM_CLEAR;
    02: signal_double_path_semaphore_APPROACH;
    00: signal_double_path_semaphore_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_semaphore, SIGNAL_ASPECT()){
    01: signal_double_onewaypath_semaphore_CLEAR;
    05: signal_double_onewaypath_semaphore_CLEAR;
    04: signal_double_onewaypath_semaphore_CLEAR;
    03: signal_double_onewaypath_semaphore_MEDIUM_CLEAR;
    02: signal_double_onewaypath_semaphore_APPROACH;
    00: signal_double_onewaypath_semaphore_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_semaphore, SIGNAL_TYPE() << 8 | param_use_double_headed_block){
    0x000: sw_single_block_semaphore;
    0x001: sw_double_block_semaphore;
    0x400..0x401: sw_double_path_semaphore;
    0x500..0x501: sw_double_onewaypath_semaphore;
    0x700..0x701: signal_noentry;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_searchlight, SIGNAL_ASPECT()){
    01: signal_single_block_searchlight_CLEAR;
    05: signal_single_block_searchlight_CLEAR;
    04: signal_single_block_searchlight_CLEAR;
    03: signal_single_block_searchlight_CLEAR;
    02: signal_single_block_searchlight_APPROACH;
    00: signal_single_block_searchlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_searchlight, SIGNAL_ASPECT()){
    01: signal_double_block_searchlight_CLEAR;
    05: signal_double_block_searchlight_ADVANCED_APPROACH_LIMITED;
    04: signal_double_block_searchlight_APPROACH_LIMITED;
    03: signal_double_block_searchlight_ADVANCED_APPROACH;
    02: signal_double_block_searchlight_APPROACH;
    00: signal_double_block_searchlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_searchlight, SIGNAL_ASPECT()){
    01: signal_double_path_searchlight_CLEAR;
    05: signal_double_path_searchlight_CLEAR;
    04: signal_double_path_searchlight_CLEAR;
    03: signal_double_path_searchlight_MEDIUM_CLEAR;
    02: signal_double_path_searchlight_APPROACH;
    00: signal_double_path_searchlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_searchlight, SIGNAL_ASPECT()){
    01: signal_double_onewaypath_searchlight_CLEAR;
    05: signal_double_onewaypath_searchlight_CLEAR;
    04: signal_double_onewaypath_searchlight_CLEAR;
    03: signal_double_onewaypath_searchlight_MEDIUM_CLEAR;
    02: signal_double_onewaypath_searchlight_APPROACH;
    00: signal_double_onewaypath_searchlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_searchlight, SIGNAL_TYPE() << 8 | param_use_double_headed_block){
    0x000: sw_single_block_searchlight;
    0x001: sw_double_block_searchlight;
    0x400..0x401: sw_double_path_searchlight;
    0x500..0x501: sw_double_onewaypath_searchlight;
    0x700..0x701: signal_noentry;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_colorlight, SIGNAL_ASPECT()){
    01: signal_single_block_colorlight_CLEAR;
    05: signal_single_block_colorlight_CLEAR;
    04: signal_single_block_colorlight_CLEAR;
    03: signal_single_block_colorlight_CLEAR;
    02: signal_single_block_colorlight_APPROACH;
    00: signal_single_block_colorlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_colorlight, SIGNAL_ASPECT()){
    01: signal_double_block_colorlight_CLEAR;
    05: signal_double_block_colorlight_ADVANCED_APPROACH_LIMITED;
    04: signal_double_block_colorlight_APPROACH_LIMITED;
    03: signal_double_block_colorlight_ADVANCED_APPROACH;
    02: signal_double_block_colorlight_APPROACH;
    00: signal_double_block_colorlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_colorlight, SIGNAL_ASPECT()){
    01: signal_double_path_colorlight_CLEAR;
    05: signal_double_path_colorlight_CLEAR;
    04: signal_double_path_colorlight_CLEAR;
    03: signal_double_path_colorlight_MEDIUM_CLEAR;
    02: signal_double_path_colorlight_APPROACH;
    00: signal_double_path_colorlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_colorlight, SIGNAL_ASPECT()){
    01: signal_double_onewaypath_colorlight_CLEAR;
    05: signal_double_onewaypath_colorlight_CLEAR;
    04: signal_double_onewaypath_colorlight_CLEAR;
    03: signal_double_onewaypath_colorlight_MEDIUM_CLEAR;
    02: signal_double_onewaypath_colorlight_APPROACH;
    00: signal_double_onewaypath_colorlight_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_colorlight, SIGNAL_TYPE() << 8 | param_use_double_headed_block){
    0x000: sw_single_block_colorlight;
    0x001: sw_double_block_colorlight;
    0x400..0x401: sw_double_path_colorlight;
    0x500..0x501: sw_double_onewaypath_colorlight;
    0x700..0x701: signal_noentry;
}

switch(FEAT_SIGNALS, SELF, sw_single_block_boposition, SIGNAL_ASPECT()){
    01: signal_single_block_boposition_CLEAR;
    05: signal_single_block_boposition_CLEAR;
    04: signal_single_block_boposition_CLEAR;
    03: signal_single_block_boposition_CLEAR;
    02: signal_single_block_boposition_APPROACH;
    00: signal_single_block_boposition_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_block_boposition, SIGNAL_ASPECT()){
    01: signal_double_block_boposition_CLEAR;
    05: signal_double_block_boposition_ADVANCED_APPROACH_LIMITED;
    04: signal_double_block_boposition_APPROACH_LIMITED;
    03: signal_double_block_boposition_ADVANCED_APPROACH;
    02: signal_double_block_boposition_APPROACH;
    00: signal_double_block_boposition_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_path_boposition, SIGNAL_ASPECT()){
    01: signal_double_path_boposition_CLEAR;
    05: signal_double_path_boposition_CLEAR;
    04: signal_double_path_boposition_CLEAR;
    03: signal_double_path_boposition_MEDIUM_CLEAR;
    02: signal_double_path_boposition_APPROACH;
    00: signal_double_path_boposition_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_double_onewaypath_boposition, SIGNAL_ASPECT()){
    01: signal_double_onewaypath_boposition_CLEAR;
    05: signal_double_onewaypath_boposition_CLEAR;
    04: signal_double_onewaypath_boposition_CLEAR;
    03: signal_double_onewaypath_boposition_MEDIUM_CLEAR;
    02: signal_double_onewaypath_boposition_APPROACH;
    00: signal_double_onewaypath_boposition_STOP;
}

switch(FEAT_SIGNALS, SELF, sw_boposition, SIGNAL_TYPE() << 8 | param_use_double_headed_block){
    0x000: sw_single_block_boposition;
    0x001: sw_double_block_boposition;
    0x400..0x401: sw_double_path_boposition;
    0x500..0x501: sw_double_onewaypath_boposition;
    0x700..0x701: signal_noentry;
}

switch(FEAT_SIGNALS, SELF, sw_signal_styles, signal_style) {
    1: sw_semaphore;
    2: sw_searchlight;
    3: sw_colorlight;
    4: sw_boposition;
}

item (FEAT_SIGNALS, custom_signals, 0) {
    property {
        extra_aspects: 4;
    }
}

switch (FEAT_SIGNALS, SELF, replaced_signals_semaphore, param_signal_gui_row_1) {
    0: sw_semaphore;
    1: sw_searchlight;
    2: sw_colorlight;
    3: sw_boposition;
}
switch (FEAT_SIGNALS, SELF, replaced_signals_electric, param_signal_gui_row_2) {
    0: sw_semaphore;
    1: sw_searchlight;
    2: sw_colorlight;
    3: sw_boposition;
}
switch (FEAT_SIGNALS, SELF, replaced_signals, SIGNAL_VARIANT()) {
    0: replaced_signals_electric;
    1: replaced_signals_semaphore;
}


if (param_replace_default_graphics) {
    item (FEAT_SIGNALS, custom_signals, 0) {
        property {
            enable_no_entry_signals: 1;
        }
        graphics {
            replaced_signals;
        }
    }
} else {
    item (FEAT_SIGNALS, custom_signals, 0) {
        property {
            define_style:       1; // semaphore
            style_name:         string(STR_SIG_SEMAPHORE);
            enable_no_entry_signals: 1;
            style_semaphore_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY, SIGNAL_TYPE_NO_ENTRY);

            define_style:       2; // searchlight
            style_name:         string(STR_SIG_SEARCHLIGHT);
            enable_no_entry_signals: 1;
            style_electric_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY, SIGNAL_TYPE_NO_ENTRY);
            
            define_style:       3; // colorlight
            style_name:         string(STR_SIG_COLORLIGHT);
            enable_no_entry_signals: 1;
            style_electric_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY, SIGNAL_TYPE_NO_ENTRY);
            
            define_style:       4; // b&o color position
            style_name:         string(STR_SIG_BOPOSITION);
            enable_no_entry_signals: 1;
            style_electric_enabled: bitmask(SIGNAL_TYPE_NORMAL, SIGNAL_TYPE_PBS, SIGNAL_TYPE_PBS_ONEWAY, SIGNAL_TYPE_NO_ENTRY);
        }
        graphics {
            sw_signal_styles;
        }
    }
}
